> 2016å¹´12æœˆ 6æ—¥ æ˜ŸæœŸäºŒ 11æ—¶58åˆ†53ç§’ CST

# RC4 ç®—æ³•
RC4 æ˜¯ä¸€ç§ç§˜é’¥é•¿åº¦å¯å˜ã€é¢å‘å­—èŠ‚æ“ä½œçš„æµå¯†ç ã€‚SSL/TLSåè®®ã€WEPã€WPAä¸­å‡æœ‰æ‰€åº”ç”¨

## RC4 Cipher
RC4 é€šè¿‡ç”Ÿæˆä¼ªéšæœºä½æµä½œä¸ºkeystream, 
- åŠ å¯†ï¼šè¯¥ keystream ä¸æ˜æ–‡ plaintext åšå¼‚æˆ–æ“ä½œä»è€Œå¾—åˆ°å¯†æ–‡ ciphertext;
- è§£å¯†ï¼šè¯¥ keystream ä¸å¯†æ–‡ ciphertext åšå¼‚æˆ–æ“ä½œä»è€Œå¾—åˆ°æ˜æ–‡ plaintext;

æ‰€ä»¥ï¼ŒRC4 çš„é‡ç‚¹åœ¨äºå¦‚ä½•ç”Ÿæˆ keystream å‘¢ï¼ŸğŸ¤”

å…¶å®ï¼Œto generate the keystream, the cipher makes use of a secret internal state which consists of two parts:
1. A permutation of all 256 possible bytes (å¯¹åº”ä¸‹é¢çš„ S)
2. Two 8-bit index-pointers (å¯¹åº”ä¸‹é¢çš„ i å’Œ j)

Anyway, ç”Ÿæˆ keystream åªè¦æœ‰ä¸¤éƒ¨åˆ†ï¼š
1. Key Scheduling Algorithm (KSA): The permuation S ä¸€å¼€å§‹ä¼šç”±ä¸€ä¸ªé•¿åº¦å¯å˜çš„ç§˜é’¥ key è¿›è¡Œåˆå§‹åŒ–ï¼ˆkey çš„é•¿åº¦ä¸€èˆ¬ä¸º40åˆ°2048 bitsï¼‰ï¼Œ
ç„¶åä½¿ç”¨ key-scheduling algorithm (KSA) å¯¹ S åšè½¬æ¢ï¼Œ
2. Pseudo-Random Generation Algorithm (PRGA): æœ€ååœ¨ä½¿ç”¨ ä¼ªéšæœºæ•°ç”Ÿæˆç®—æ³• (PRGA) æ¥ç”Ÿæˆ keystream.


### Key-Scheduling Algorithm (KSA)
- S: The key-scheduling algorithm is used initialize the permutation in the array S. 
- len(key): the number of bytes in the key, é•¿åº¦èŒƒå›´å¯ä»¥ä¸º [1, 256]ï¼Œä¸è¿‡ä¸€èˆ¬æ˜¯ [5, 256].

ç®—æ³•å¯ä»¥è¡¨ç¤ºå¦‚ä¸‹ï¼š
```golang
var S [256]uint32

for i := 0; i < 256; i++ {
  S[i] = uint32(i)
}

var j uint8 = 0
for i := 0; i < 256; i++{
  j += S[i] + key[i%len(key)]
  S[i], S[j] = S[j], S[i]
}
```

### Pseudo-Random Generation Algorithm (PRGA)
- src: the plaintext or the ciphertext
- keystream: the keystream

ç®—æ³•å¯ä»¥è¡¨ç¤ºå¦‚ä¸‹ï¼š
```golang
var i uint8 = 0
var j uint8 = 0
var keystream []byte
for k, v := range src {
  i += 1
  j += uint8(S[i])
  S[i], S[j] = S[j], S[i]
  keysream[k] = S[ uint8(S[i] + S[j]) ]
}
```
ğŸ˜ï¼Œæœ‰æ²¡æœ‰å‘ç°ï¼Œå…¶å®ä¸Šé¢çš„keystreamå¯ä»¥ä½œä¸ºéšæœºæ•°ï¼Œå› æ­¤ä¹Ÿå¯ä»¥æŠŠ åŸºäº RC4 æ¥å®ç° PRNG

## RC4 cipher çš„å®ç°
go è¯­è¨€ä¸­çš„ crypto/rc4 å®ç°äº†rc4 cipherï¼Œä»£ç å¦‚ä¸‹ï¼š

```golang
import "strconv"

// A Cipher is an instance of RC4 using a particular key.
type Cipher struct {
  s [256]uint32
  i, j uint8
}

type KeySizeError int

func (k KeySizeError) Error() string {
  return "crypto/rc4: invalid key" + strconv.Itoa(int(k))
}

// NewCipher creates and returns a new Cipher. The argument should be the RC4 key,
// at least 1 byte and at most 256 bytes.
func NewCipher(key []byte) (*Cipher, error){
  k := len(key)
  if k<1 || k>256 {
    return nil, KeySizeError(k)
  }
  
  // Key-scheduling algorithm (KSA)
  // 1. initialize s as identity permutation
  var c Cipher
  for i:=0; i<256; i++ {
    c.s[i] = uint32(i)
  }
  // 2. mixes s with the bytes of the key
  var j uint8 = 0
  for i:=0; i<256; i++ {
    j += uint8(c.s[i]) + key[i%k]
    c.s[i], c.s[j] = c.s[j], c.s[i]
  }
  return &c, nil
}

// Reset zeros the key data so that it will no longer appear in the process's memory.
func (c *Cipher) Reset() {
  for i := range c.s {
    c.s[i] = 0
  }
  c.i, c.j = 0, 0
}

//xorKeyStreamGeneric set dst to the result of XORing src with the key stream.
// Dst and src may be the same slice but otherwise should not overlap
//
// This is the pure Go version. rc4_{amd64, 386, arm}* contain assembly implementations.
// This is here for tests and to prevent bitrot.
func (c *Cipher) xorKeyStreamGeneric(dst, src []byte) {
  i, j := c.i, c.j
  for k, v := range src {
    i += 1
    j += uint8(c.s[i])
    c.s[i], c.s[j] = c.s[j], c.s[i]
    dst[k] = v ^ uint8(c.s[uint8(c.s[i]+c.s[j])])
  }
  c.i, c.j = i, j
}
```

ä¸æ±‡ç¼–è¯­è¨€çš„æ¥å£å¦‚ä¸‹ï¼š
```golang
// +build amd64 amd64p32 arm,!nacl 386

func xorKeyStream(dst, src *byte, n int, state *[256]uint32, i, j *uint8)

// XORKeyStream sets dst to the result of XORing src with the key stream.
// Dst and src may be the same slice but otherwise should not overlap.
func (c *Cipher) XORKeyStream(dst, src []byte) {
  if len(src) == 0 {
    return 
  }
  xorKeySteream(&dst[0], &src[0], len(src), &c.s, &c.i, &c.j)
}
```

å¯¹åº” [AMD64 å¤„ç†å™¨ä¸Šçš„æ±‡ç¼–è¯­è¨€çš„ä¼˜åŒ–ç‰ˆ](http://www.zorinaq.com/papers/rc4-amd64.html)å®ç°å¦‚ä¸‹ï¼š
```arm
// RC4 implementation optimized for AMD64 processors.
TEXT .xorKeyStream(SB),NOSPLIT, $0
  MOVQ n+16(FP), BX   // rbx = ARG(len)
  MOVQ src+8(FP), SI  // in = ARG(in)
  MOVQ dst+0(FP), DI  // out = ARG(out)
  MOVQ state+24(FP), BP
  MOVQ i+32(FP), AX
  MOVBQZX 0(AX), CX   // x = *xp
  MOVQ j+40(FP), AX
  MOVBQZX 0(AX), DX   // y = *yp

  LEAQ (SI)(BX*1), R9 //limit = in + len

l1:	CMPQ	SI,		R9		// cmp in with in+len
	JGE	finished			  // jump if (in >= in+len)

	INCB	CX
	EXTEND(CX)
	TESTL	$15,		CX
	JZ	wordloop

	MOVBLZX	(BP)(CX*4),	AX

	ADDB	AX,		DX		  // y += tx
	EXTEND(DX)
	MOVBLZX	(BP)(DX*4),	BX		// ty = d[y]
	MOVB	BX,		(BP)(CX*4)	  // d[x] = ty
	ADDB	AX,		BX	        	// val = ty+tx
	EXTEND(BX)
	MOVB	AX,		(BP)(DX*4)	  // d[y] = tx
	MOVBLZX	(BP)(BX*4),	R8		// val = d[val]
	XORB	(SI),		R8		      // xor 1 byte
	MOVB	R8,		(DI)
	INCQ	SI				      // in++
	INCQ	DI				      // out++
	JMP l1

wordloop:
	SUBQ	$16,		R9
	CMPQ	SI,		R9
	JGT	end

start:
	ADDQ	$16,		SI		// increment in
	ADDQ	$16,		DI		// increment out


	// Each KEYROUND generates one byte of key and
	// inserts it into an XMM register at the given 16-bit index.
	// The key state array is uint32 words only using the bottom
	// byte of each word, so the 16-bit OR only copies 8 useful bits.
	// We accumulate alternating bytes into X0 and X1, and then at
	// the end we OR X1<<8 into X0 to produce the actual key.
	//
	// At the beginning of the loop, CX%16 == 0, so the 16 loads
	// at state[CX], state[CX+1], ..., state[CX+15] can precompute
	// (state+CX) as R12 and then become R12[0], R12[1], ... R12[15],
	// without fear of the byte computation CX+15 wrapping around.
	//
	// The first round needs R12[0], the second needs R12[1], and so on.
	// We can avoid memory stalls by starting the load for round n+1
	// before the end of round n, using the LOAD macro.
	LEAQ	(BP)(CX*4),	R12

#define KEYROUND(xmm, load, off, r1, r2, index) \
	MOVBLZX	(BP)(DX*4),	R8; \
	MOVB	r1,		(BP)(DX*4); \
	load((off+1), r2); \
	MOVB	R8,		(off*4)(R12); \
	ADDB	r1,		R8; \
	EXTEND(R8); \
	PINSRW	$index, (BP)(R8*4), xmm

#define LOAD(off, reg) \
	MOVBLZX	(off*4)(R12),	reg; \
	ADDB	reg,		DX; \
	EXTEND(DX)

#define SKIP(off, reg)

	LOAD(0, AX)
	KEYROUND(X0, LOAD, 0, AX, BX, 0)
	KEYROUND(X1, LOAD, 1, BX, AX, 0)
	KEYROUND(X0, LOAD, 2, AX, BX, 1)
	KEYROUND(X1, LOAD, 3, BX, AX, 1)
	KEYROUND(X0, LOAD, 4, AX, BX, 2)
	KEYROUND(X1, LOAD, 5, BX, AX, 2)
	KEYROUND(X0, LOAD, 6, AX, BX, 3)
	KEYROUND(X1, LOAD, 7, BX, AX, 3)
	KEYROUND(X0, LOAD, 8, AX, BX, 4)
	KEYROUND(X1, LOAD, 9, BX, AX, 4)
	KEYROUND(X0, LOAD, 10, AX, BX, 5)
	KEYROUND(X1, LOAD, 11, BX, AX, 5)
	KEYROUND(X0, LOAD, 12, AX, BX, 6)
	KEYROUND(X1, LOAD, 13, BX, AX, 6)
	KEYROUND(X0, LOAD, 14, AX, BX, 7)
	KEYROUND(X1, SKIP, 15, BX, AX, 7)
	
	ADDB	$16,		CX

	PSLLQ	$8,		X1
	PXOR	X1,		X0
	MOVOU	-16(SI),	X2
	PXOR	X0,		X2
	MOVOU	X2,		-16(DI)

	CMPQ	SI,		R9		  // cmp in with in+len-16
	JLE	start				    // jump if (in <= in+len-16)

end:
	DECB	CX
	ADDQ	$16,		R9		// tmp = in+len

	// handle the last bytes, one by one
l2:	CMPQ	SI,		R9		// cmp in with in+len
	JGE	finished			  // jump if (in >= in+len)

	INCB	CX
	EXTEND(CX)
	MOVBLZX	(BP)(CX*4),	AX

	ADDB	AX,		DX		        // y += tx
	EXTEND(DX)
	MOVBLZX	(BP)(DX*4),	BX		// ty = d[y]
	MOVB	BX,		(BP)(CX*4)	  // d[x] = ty
	ADDB	AX,		BX		        // val = ty+tx
	EXTEND(BX)
	MOVB	AX,		(BP)(DX*4)	  // d[y] = tx
	MOVBLZX	(BP)(BX*4),	R8		// val = d[val]
	XORB	(SI),		R8	      	// xor 1 byte
	MOVB	R8,		(DI)
	INCQ	SI				          // in++
	INCQ	DI			          	// out++
	JMP l2

finished:
	MOVQ	j+40(FP),	BX
	MOVB	DX, 0(BX)
	MOVQ	i+32(FP),	AX
	MOVB	CX, 0(AX)
	RET

```

### RC4 cipher çš„ä½¿ç”¨
é¦–å…ˆè°ƒç”¨rc4åŒ…ä¸­çš„`func NewCipher(key []byte) (*Cipher, error)`æ–°ç”Ÿæˆä¸€ä¸ªCipher, 
  ç„¶åè°ƒç”¨`func (c *Cipher) XORKeyStream(dst, src []byte)`è¿›è¡ŒåŠ å¯†æˆ–è€…è§£å¯†

